# Copyright (c) 2025 vivo Mobile Communication Co., Ltd.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/default_targets.gni")
import("//build/templates/board.gni")

blueos_board("qemu_virt64_aarch64") {
  kernel_config = {
    _linker_script = "//kernel/kernel/src/boards/${board}/link.x"
    cflags = [ "--static" ]
    ldflags = [
      "-nostartfiles",
      "-Wl,-gc-sections",
      "-Wl,-T" + rebase_path(_linker_script),
    ]
    rustflags = [
      "--cfg",
      "target_board=\"qemu_virt64_aarch64\"",
      "-Zemit-stack-sizes",
      "-Crelocation-model=static",
      "-Clinker=aarch64-none-elf-gcc",
    ]
    foreach(item, ldflags) {
      rustflags += [ "-Clink-arg=${item}" ]
    }
    inputs = [ _linker_script ]
  }

  app_config = {
    rustflags = [
      "--cfg",
      "target_board=\"qemu_riscv64\"",
      "-Clinker=aarch64-none-elf-gcc",
    ]
  }

  default = [
    "//apps/example:check_apps",
    "//apps/shell:shell_runner",
  ]
  _check_all = check_all_on_virtual_board + clippy_all_on_virtual_board
  check_all = default + filter_exclude(_check_all, [ "//kernel/adapter*" ])
}
