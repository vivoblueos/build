# Copyright (c) 2025 vivo Mobile Communication Co., Ltd.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/default_targets.gni")
import("//build/templates/board.gni")

_sysroot = exec_script("//build/scripts/get_sysroot.py",
                       [ "arm-none-eabi-gcc" ],
                       "string")

blueos_board("qemu_mps2_an385") {
  kernel_config = {
    _linker_script =
        exec_script("//build/scripts/preprocess_linker_script.py",
                    [
                      "--preprocesser",
                      "arm-none-eabi-gcc",
                      "--input",
                      rebase_path("//kernel/arch/arm/cortex_m/link.ld"),
                      "--include_dir",
                      rebase_path("${root_build_dir}"),
                      "--output",
                      rebase_path("${root_build_dir}/link.pre.ld"),
                    ],
                    "string",
                    [
                      rebase_path("${root_build_dir}/autoconf.h"),
                      rebase_path("//kernel/arch/arm/cortex_m/link.ld"),
                    ])
    cflags = [
      "--target=thumbv7m-none-eabi",
      "--static",
      "-fshort-enums",
      "-fsigned-char",
      "-nostdlib",
      "-isystem",
      "${_sysroot}/include",
    ]
    ldflags = [
      "-nostartfiles",
      "-Wl,-gc-sections",
      "-Wl,-T" + rebase_path(_linker_script),
      "-Wl,-z,norelro",
    ]
    rustflags = [
      "--cfg",
      "target_board=\"qemu_mps2_an385\"",
      "--cfg",
      "cortex_m",
      "--cfg",
      "armv7m",
      "-Zemit-stack-sizes",
      "-Crelocation-model=static",
      "-Clinker=arm-none-eabi-gcc",
    ]
    foreach(item, ldflags) {
      rustflags += [ "-Clink-arg=${item}" ]
    }
    inputs = [ "//kernel/arch/arm/cortex_m/link.ld" ]
  }
  app_config = {
    rustflags = [
      "-Clinker=clang",
      "-Clink-arg=-fuse-ld=lld",
      "-Clink-arg=--target=thumbv7m-none-eabi",
      "-Clink-arg=-Wl,-z,norelro",
    ]
  }
  default = [
    "//apps/example:check_apps",
    "//apps/shell:shell_runner",
  ]
  check_all = default + check_all_on_virtual_board + clippy_all_on_virtual_board
  check_all += [ "//kernel/adapter/cmsis:run_adapter_validation" ]
  check_all += [ "//kernel/adapter/thread_metric:run_tests" ]
}
