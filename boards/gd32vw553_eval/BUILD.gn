# Copyright (c) 2025 vivo Mobile Communication Co., Ltd.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/default_targets.gni")
import("//build/templates/board.gni")

_sysroot = exec_script("//build/scripts/get_sysroot.py",
                       [ "riscv-none-elf-gcc" ],
                       "string")

blueos_board("gd32vw553_eval") {
  kernel_config = {
    _linker_script = "//kernel/kernel/src/boards/${board}/link.x"
    cflags = [
      "--target=riscv-none-elf",
      "--static",
      "-fshort-enums",
      "-fsigned-char",
      "-nostdlib",
      "-isystem",
      "${_sysroot}/include",
    ]
    ldflags = [
      "-nostartfiles",
      "-Wl,-gc-sections",
      "-Wl,-T" + rebase_path(_linker_script),
      "-Wl,-z,norelro",
    ]
    rustflags = [
      "--cfg",
      "target_board=\"gd32vw553_eval\"",
      "-Zemit-stack-sizes",
      "-Crelocation-model=static",
      "-Clinker=riscv-none-elf-gcc",
    ]
    foreach(item, ldflags) {
      rustflags += [ "-Clink-arg=${item}" ]
    }
    inputs = [ _linker_script ]
  }

  app_config = {
    rustflags = [
      "--cfg",
      "target_board=\"gd32vw553_eval\"",
      "-Clinker=clang",
      "-Clink-arg=-fuse-ld=lld",
      "-Clink-arg=--target=riscv32",
      "-Clink-arg=-march=rv32",
      "-Clink-arg=-mabi=ilp32",
      "-Clink-arg=-Wl,-z,norelro",
    ]
  }
  default = [
    "//apps/shell",
    "//apps/example/blinky:blinky_example",
  ]
  check_all = []
}
